export PATH=/usr/share/GSC/bin:$HOME/bin:/usr/local/bin:$PATH
export ZSH="$HOME/.oh-my-zsh"
export LC_CTYPE=en_US.UTF-8
export SYSTEMD_TIMEDATED_NTP_SERVICES=chronyd.service

ZSH_THEME="af-magic"
ZSH_CUSTOM=$HOME/.astroarch

EXTRA_ZSH=$HOME/extra.zsh

zstyle ':omz:update' mode disabled

ENABLE_CORRECTION="false"
HIST_STAMPS="yyyy-mm-dd"

plugins=(git archlinux)

# Find all plugins and append them to plugins
for dir in $HOME/.astroarch/plugins/**/*.plugin.zsh; do
    split=(${(s:/:)dir})
    last=$split[-1]
    name=(${(s:.:)last})
    plugins+=($name[1])
done;

source $ZSH/oh-my-zsh.sh

EDITOR=nano
INDI_ROLLBACK_VERSION=2.1.3-1
INDI_LIBS_ROLLBACK_VERSION=2.1.3-1
INDI_DRIVERS_ROLLBACK_VERSION=2.1.3-1
KSTARS_ROLLBACK_VERSION=3.7.6-2

# Alias section
alias update-astromonitor='wget -O - https://raw.githubusercontent.com/MattBlack85/astro_monitor/main/install.sh | sh'
alias astro-rollback-full='astro-rollback-indi && astro-rollback-kstars'
alias apt=xyz
alias apt-get=xyz

# Run aa_motd.sh
bash /home/astronaut/.astroarch/scripts/aa_motd.sh

function use-astro-bleeding-edge()
{
    sudo pacman -Sy --noconfirm && yes | LC_ALL=en_US.UTF-8 sudo pacman -S kstars-git libindi-git indi-3rdparty-drivers-git indi-3rdparty-libs-git --noconfirm
}

function use-astro-stable()
{
    sudo pacman -Sy --noconfirm && yes | LC_ALL=en_US.UTF-8 sudo pacman -S kstars libindi indi-3rdparty-drivers indi-3rdparty-libs --noconfirm
}


function astro-rollback-indi()
{
    setopt localoptions rmstarsilent
    mkdir -p ~/.rollback
    cd ~/.rollback
    wget -O indi-3rdparty-drivers-${INDI_DRIVERS_ROLLBACK_VERSION}-aarch64.pkg.tar.xz http://astromatto.com:9000/aarch64/indi-3rdparty-drivers-${INDI_DRIVERS_ROLLBACK_VERSION}-aarch64.pkg.tar.xz
    wget -O libindi-${INDI_ROLLBACK_VERSION}-aarch64.pkg.tar.xz http://astromatto.com:9000/aarch64/libindi-${INDI_ROLLBACK_VERSION}-aarch64.pkg.tar.xz
    wget -O indi-3rdparty-libs-${INDI_LIBS_ROLLBACK_VERSION}-aarch64.pkg.tar.xz http://astromatto.com:9000/aarch64/indi-3rdparty-libs-${INDI_LIBS_ROLLBACK_VERSION}-aarch64.pkg.tar.xz
    sudo pacman -U libindi* indi* --noconfirm
    cd - > /dev/null 2>&1
    rm -rf ~/.rollback/*
}

function astro-rollback-kstars()
{
    setopt localoptions rmstarsilent
    mkdir -p ~/.rollback
    cd ~/.rollback
    wget -O kstars-${KSTARS_ROLLBACK_VERSION}-aarch64.pkg.tar.xz http://astromatto.com:9000/aarch64/kstars-${KSTARS_ROLLBACK_VERSION}-aarch64.pkg.tar.xz
    sudo pacman -U kstars* --noconfirm
    cd - > /dev/null 2>&1
    rm -rf ~/.rollback/*
}

function update-astroarch()
{
    # Store actual version
    OLD_VER=$(cat /home/$USER/.astroarch.version)

    # Store the current commit hash before the pull
    cd /home/$USER/.astroarch
    CURRENT_COMMIT=$(git rev-parse HEAD)

    # Checkout latest changes from git
    cd /home/$USER/.astroarch
    git pull origin main
    cd - > /dev/null 2>&1

    NEW_VER=$(cat /home/$USER/.astroarch/configs/.astroarch.version)

    if [[ "$OLD_VER" != "$NEW_VER" ]]; then
        zsh /home/$USER/.astroarch/scripts/$NEW_VER.sh
        if [[ $? -ne 0 ]]; then
            # Revert to the commit stored before the pull
            cd /home/$USER/.astroarch
            git reset --hard "$CURRENT_COMMIT"
            cd - > /dev/null 2>&1
            notify-send --app-name 'AstroArch' --icon="/home/astronaut/.astroarch/assets/icons/novnc-icon.svg" -t 10000 'Update AstroArch' "Script '$SCRIPT_VER' failed. Reverted to previous state."
        fi
        notify-send --app-name 'AstroArch' --icon="/home/astronaut/.astroarch/assets/icons/novnc-icon.svg" -t 10000 'Update AstroArch' 'All scripts applied successfully'
    fi;

    # Temporary fix for kde-portal duplicated conf
    if [ -f /usr/share/xdg-desktop-portal/kde-portal.conf ]; then
        sudo mv /usr/share/xdg-desktop-portal/kde-portal.conf /usr/share/xdg-desktop-portal/kde-portal.conf.old
    fi;

    # Update the repo content
    sudo pacman -Sy --noconfirm

    # Update always keyring first, than all of the other packages
    sudo pacman -S archlinux-keyring --noconfirm

    # Testing libraries before updating
    # --- Configuration ---
    BACKUP_DIR="$HOME/pacman_backups"
    WATCHLIST_FILE="$HOME/.astroarch/configs/astroarch-maintained-packages-list.txt"
    mkdir -p "$BACKUP_DIR"
    DATE=$(date +%F_%H-%M)

    # --- Loading WATCHLIST ---
    if [[ -f "$WATCHLIST_FILE" ]]; then
        WATCHLIST=( ${(f)"$(grep -v '^#' "$WATCHLIST_FILE" | sed '/^$/d')"} )
        echo "‚úÖ Watchlist loaded : ${#WATCHLIST} packages found"
    else
        echo "‚ùå Error: File $WATCHLIST_FILE not found"
        return 1
    fi

    # Saving the current state of packages
    pacman -Q > "$BACKUP_DIR/full_snapshot_$DATE.txt"

    # Simulation of updates
    updates=$(checkupdates)
    if [ -z "$updates" ]; then
        echo "‚úÖ System already up to date"
        notify-send --app-name 'AstroArch' --icon="/home/astronaut/.astroarch/assets/icons/novnc-icon.svg" -t 10000 'Update AstroArch' "‚úÖ System already up to date"
        return 0
    fi

    upgrading_names=$(echo "$updates" | awk '{print $1}')

    # Analysis of dependencies
    for pkg in $WATCHLIST; do
        if ! pacman -Qi "$pkg" &> /dev/null; then
            echo "‚ö†Ô∏è  [Info] $pkg is not installed on the system"
            continue
        fi

        if [[ ${upgrading_names[(i)$pkg]} -le ${#upgrading_names} ]]; then
        echo "üü¢ $pkg: Included in the update (Low priority: will be synchronized)"
        else

        deps=(${(f)"$(pactree -u "$pkg" | grep -v "$pkg")"})

        found_risk=0
        for dep in $deps; do
            # Si une de ses d√©pendances est mise √† jour
            if [[ ${upgrading_names[(i)$dep]} -le ${#upgrading_names} ]]; then
                new_ver=$(echo $updates | grep ^$dep | awk '{print $3}')
                old_ver=$(pacman -Q "$dep" | awk '{print $2}')
                echo "‚ö†Ô∏è RISK: The dependency ‚Äò$dep‚Äô will change ($old_ver -> $new_ver)"
                echo "$pkg: may no longer be able to find its libraries"
                found_risk=1
            fi
        done
        [[ $found_risk -eq 0 ]] && echo "‚úÖ $pkg : No dependency conflicts detected"
        fi
    done

    if [ $found_risk -eq 1 ]; then
        echo "‚ùó Warning: Some critical packages have dependencies that will change. The update cannot be performed"
        notify-send --app-name 'AstroArch' --icon="/home/astronaut/.astroarch/assets/icons/novnc-icon.svg" -t 10000 'Update AstroArch' "‚ùå Warning: Some critical packages have dependencies that will change. The update cannot be performed"
        return 0
    else

    # Now upgrade all system packages, but ask user to choose in case of conflicts/choices
    sudo pacman -Syu --noconfirm

    if [ $? -eq 0 ]; then
    notify-send --app-name 'AstroArch' --icon="/home/astronaut/.astroarch/assets/icons/novnc-icon.svg" -t 10000 'Update AstroArch' "‚úÖ Successful update"
    else
    notify-send --app-name 'AstroArch' --icon="/home/astronaut/.astroarch/assets/icons/novnc-icon.svg" -t 10000 'Update AstroArch' "‚ùå Update failed"
    fi

    # Reinstall plasma-x11-session, cannot work on 1.9.0 cause of old kwin
    sudo pacman -Sy plasma-x11-session --noconfirm
    fi
}

function xyz () {
    echo "ATTENTION! The system is going to be infected with a virus now!"
    sleep 1
    echo "This virus is called ArchLinux"
    sleep 0.5
    echo -n "Transfering virus."
    sleep 0.5
    echo -n "."
    sleep 0.5
    echo -n "."
    sleep 0.5
    echo -n "."
    sleep 0.5
    echo -n "."
    echo ""
    sleep 1
    echo "Virus transfered successfully"
    echo "ENJOY YOUR ARCHLINUX SYSTEM AND EMBRACE THE DARK SIDE"
    sleep 1
    echo "That was a joke of course, there is no apt nor apt-get on arch, just pacman!"
}

if [ -f $EXTRA_ZSH ]; then
    source $EXTRA_ZSH
fi
